theory Types

  use int.Int

  (* default type mapping *)
  type key       = string
  type key_hash  = int
  type role      = string
  type address   = string
  type tez       = int
  type date      = int
  type bytes     = string
  type signature = string
  type chain_id  = int
  type uint      = int

  exception Break
  exception NotFound
  exception KeyExist
  exception NegAssignNat
  exception InvalidCaller
  exception InvalidCondition
  exception NoTransfer
  exception InvalidState
  exception Invalid string
end

module Utils
  use int.Int
  use Types
  use option.Option

  (* ------------------------------------------------------------------------ *)
  (* Tuples *)

  let function fst (a : ('t1, 't2)) : 't1
  =
  match a with
  | (l,_) -> l
  end

  let function snd (a: ('t1, 't2)) : 't2
  =
  match a with
  | (_,r) -> r
  end

  let function snd_opt (a: option ('t1, 't2)) : option 't2
  =
  match a with
  | Some (_,r) -> Some r
  | None -> None
  end

  let function nth1_of_2 (a: ('t1,'t2))     : 't1 = match a with | (v,_) -> v end
  let function nth1_of_3 (a: ('t1,'t2,'t3)) : 't1 = match a with | (v,_,_) -> v end
  let function nth1_of_4 (a: ('t1,'t2,'t3,'t4)) : 't1 = match a with | (v,_,_,_) -> v end
  let function nth1_of_5 (a: ('t1,'t2,'t3,'t4,'t5)) : 't1 = match a with | (v,_,_,_,_) -> v end

  let function nth2_of_2 (a: ('t1,'t2))     : 't2 = match a with | (_,v) -> v end
  let function nth2_of_3 (a: ('t1,'t2,'t3)) : 't2 = match a with | (_,v,_) -> v end
  let function nth2_of_4 (a: ('t1,'t2,'t3,'t4)) : 't2 = match a with | (_,v,_,_) -> v end
  let function nth2_of_5 (a: ('t1,'t2,'t3,'t4,'t5)) : 't2 = match a with | (_,v,_,_,_) -> v end

  let function nth3_of_3 (a: ('t1,'t2,'t3)) : 't3 = match a with | (_,_,v) -> v end
  let function nth3_of_4 (a: ('t1,'t2,'t3,'t4)) : 't3 = match a with | (_,_,v,_) -> v end
  let function nth3_of_5 (a: ('t1,'t2,'t3,'t4,'t5)) : 't3 = match a with | (_,_,v,_,_) -> v end

  let function nth4_of_4 (a: ('t1,'t2,'t3,'t4)) : 't4 = match a with | (_,_,_,v) -> v end
  let function nth4_of_5 (a: ('t1,'t2,'t3,'t4,'t5)) : 't4 = match a with | (_,_,_,v,_) -> v end

  let function nth5_of_5 (a: ('t1,'t2,'t3,'t4,'t5)) : 't5 = match a with | (_,_,_,_,v) -> v end

  (* ------------------------------------------------------------------------ *)
  (* Math *)

  let function abs (a : int) : int
  ensures { result >= 0 }
  =
  if a >= 0 then a else -a

  use int.EuclideanDivision

  let function floor (a : (int,int)) : int
  requires { not (snd a) = 0 }
  =
  match a with
  | (n,d) ->
   if d > 0 then
     div n d
   else
     (div n d) - 1
  end

  let function ceil (a : (int,int)) : int
  requires { not (snd a) = 0 }
  =
  match a with
  | (n,d) ->
   if d > 0 then
     (div n d) + 1
   else
     div n d
  end

  (* ------------------------------------------------------------------------ *)
  (* Rational *)

  type op_cmp =
   | OpCmpLt
   | OpCmpLe
   | OpCmpGt
   | OpCmpGe

  let function rat_cmp (op : op_cmp) (lhs : (int, int)) (rhs : (int, int)) : bool
  =  let a = (fst lhs) * (snd rhs) in
     let b = (snd lhs) * (fst rhs) in
     let pos = (fst lhs) * (fst rhs) > 0 in
     match op with
     | OpCmpLt -> if pos then
                    a < b
                  else
                    a > b
     |OpCmpLe -> if pos then
                   a <= b
                 else
                   a >= b
     |OpCmpGt -> if pos then
                   a > b
                 else
                   a < b
     |OpCmpGe -> if pos then
                   a >= b
                 else
                   a <= b
     end

  let function rat_eq (lhs : (int, int)) (rhs : (int, int)) : bool
  = fst lhs * snd rhs = fst rhs * snd lhs

  type op_arith =
   | OpArithPlus
   | OpArithMinus
   | OpArithMult
   | OpArithDiv

  let function rat_arith (op : op_arith) (lhs : (int, int)) (rhs : (int, int)) : (int, int)
  =  match op with
     | OpArithPlus -> (((fst lhs) * (snd rhs)) + ((snd rhs) * (snd lhs)) , (snd lhs) * (snd rhs))
     | OpArithMinus -> (((fst lhs) * (snd rhs)) - ((snd rhs) * (snd lhs)) , (snd lhs) * (snd rhs))
     | OpArithMult -> ((fst lhs) * (fst rhs) , (snd lhs) * (snd rhs))
     | OpArithDiv -> ((fst lhs) * (snd rhs) , (snd lhs) * (fst rhs))
     end

  let function div_tez (a : tez) (b : tez) : tez
  requires { not b = 0 }
   = div a b

  let function rat_tez (c : (int, int)) (t : tez) : tez
  requires { not (snd c) = 0 }
  =  div ((abs (fst c)) * t) (abs (snd c))

  let function abs_rat (a : (int, int)) : (int, int)
  =
  match a with
  | (l,r) -> (abs l, abs r)
  end

  let function int_to_rat (a : int) : (int, int)
  = (a,1)

  let function rat_uminus (r : (int, int)) : (int, int)
  =
  match r with
  | (l,r) -> (-l,r)
  end

  let function rat_min (a : (int, int)) (b : (int, int)) : (int, int)
  = if rat_cmp OpCmpLe a b then a else b

  let function rat_max (a : (int, int)) (b : (int, int)) : (int, int)
  = if rat_cmp OpCmpLe a b then b else a

  let function rat_dur (a : (int, int)) (b : int) : int
  requires { not (snd a) = 0 }
  = div ((fst a) * b) (snd a)

  (* ----------------------------------------------------------------------- *)
  (* String *)

  use string.String as S

  val function str_concat string string : string

  axiom str_concat_spec : forall s1 s2.
  str_concat s1 s2 = S.concat s1 s2

  val function substring string int int : string

  axiom substring_spec : forall s i j.
  substring s i j = S.substring s i j

  val function str_length string : int

  axiom str_length_spec : forall s.
  str_length s = S.length s

  val function str_eq (_ : string) (_ : string) : bool

  axiom str_eq_spec : forall s1 s2.
  str_eq s1 s1 <-> s1 = s2

  val function str_le string string : bool

  axiom str_le_spec : forall s1 s2.
  str_le s1 s2 = S.le s1 s2

  val function str_ge string string : bool

  axiom str_ge_spec : forall s1 s2.
  str_ge s1 s2 = not (S.le s1 s2) \/ s1 = s2

  val function str_lt string string : bool

  axiom str_lt_spec : forall s1 s2.
  str_lt s1 s2 = S.lt s1 s2

  val function str_gt string string : bool

  axiom str_gt_spec : forall s1 s2.
  str_gt s1 s2 = not (S.le s1 s2)

  (* ----------------------------------------------------------------------- *)
  (* Option *)

  let function issome (v : option 'a) : bool
  =
  match v with
  | Some _ -> true
  | None -> false
  end

  let function isnone (v : option 'a) : bool
  =
  match v with
  | Some _ -> false
  | None -> true
  end

  let getopt (o : option 'a) : 'a
  raises { NotFound }
  =
  match o with
  | Some v -> v
  | None -> raise NotFound
  end

  (* ----------------------------------------------------------------------- *)
  (* crypto *)
  let function blake2b (b : bytes) : bytes = b
  let function sha256 (b : bytes) : bytes = b
  let function sha512 (b : bytes) : bytes = b
  let function hash_key (_k : key) : key_hash = 0
  let check_signature (_k : key) (_s : signature) (_b : bytes) = true

  (* ----------------------------------------------------------------------- *)
  (* serialization *)
  val function pack (_x : 'a) : bytes
  val function unpack bytes : option 'a

  (* ----------------------------------------------------------------------- *)
  (* bool *)
  let function neq_bool (a : bool) (b : bool) : bool = not (a && b)

  (* ------------------------------------------------------------------------ *)
  axiom inv_pack_unpack: forall x : bytes. exists a : int. unpack(x) = Some(a) -> pack(a) = x

end

module Trace

  use Types
  use list.List
  use list.Mem
  use option.Option

  type _asset
  type _entry
  type _field

  type _action =
  | TrRm_ _asset
  | TrAdd_ _asset
  | TrUpdate_ _field
  | TrGet_ _asset
  | TrIterate _asset
  | TrTransfer int
  | TrCallContract int

  type _traces = list _action

  predicate changes_performed_by (changes : _traces) (tr : _traces) (p : bool) =
  forall x : _action.
  mem x tr ->
  mem x changes ->
  p

  predicate performed_by  (tr : _traces) (p : bool) =
  forall x : _action.
  mem x tr ->
  p

end

theory Operation

  use Types
  use int.Int
  use list.List
  use option.Option
  use Utils

  type call = {
    _name : string;
    _args : list string;
  }

  let _eq_call (c1 : call) (c2 : call) : bool
  =  str_eq c1._name c2._name (* TODO : complete *)

  type operation = {
     _destination_ : address;
     _amount_      : tez;
     _call_        : option call;
  }

  let function _eq_operation (o1 : operation) (o2 : operation)
  = str_eq o1._destination_ o2._destination_ &&
    o1._amount_ = o2._amount_ &&
    match o1._call_, o2._call_ with
    | Some c1, Some c2 -> _eq_call c1 c2
    | _ -> false
    end


  let _mk_transfer (d : address) (a : tez) : operation = {
    _destination_ = d;
    _amount_ = a;
    _call_   = None;
  }

  let _mk_call (d : address) (a : tez) (n : string) (l : list string) : operation = {
    _destination_ = d;
    _amount_ = a;
    _call_ = Some { _name = n; _args = l };
  }

  type entrysig = {
    _entry_name : string;
    _entry_dest : address;
  }

  let function entrypoint (n : string) (d : address) : option entrysig = Some {
    _entry_name = n;
    _entry_dest = d;
  }

  let _mk_operation (a : tez) (e : entrysig) (l : list string) : operation = {
    _destination_ = e._entry_dest;
    _amount_ = a;
    _call_ = Some { _name = e._entry_name; _args = l };
  }

end

(* source : http://toccata.lri.fr/gallery/insertion_sort_list.fr.html *)
module InsertionSort

  type elt
  val predicate le elt elt

  clone relations.TotalPreOrder with
    type t = elt, predicate rel = le, axiom .
  clone export list.Sorted with
    type t = elt, predicate le  = le, goal Transitive.Trans

  use list.List
  use list.Permut

  let rec function insert (x: elt) (l: list elt) : list elt
    requires { sorted l }
    ensures  { sorted result }
    ensures  { permut (Cons x l) result }
    variant  { l }
  = match l with
    | Nil -> Cons x Nil
    | Cons y r -> if le x y then Cons x l else Cons y (insert x r)
    end

  let rec function insertion_sort (l: list elt) : list elt
    ensures { sorted result }
    ensures { permut l result }
    variant { l }
  = match l with
    | Nil -> Nil
    | Cons x r -> insert x (insertion_sort r)
    end

end

module Undup

  use list.List
  use list.Mem as M
  use list.Length as L
  use fset.Undup as U

  (* ------------------------------------------------------------------------ *)
  type t

  (* ------------------------------------------------------------------------ *)
  val function eqt t t : bool

  (* ------------------------------------------------------------------------ *)
  axiom eqP : forall x y. eqt x y <-> x = y

  (* ------------------------------------------------------------------------ *)
  let rec function mem (a : t) (l : list t) : bool
  variant { l }
  ensures { result <-> M.mem a l }
  =
    match l with
    | Nil -> false
    | Cons e tl -> eqt a e || mem a tl
    end

  (* ------------------------------------------------------------------------ *)
  let rec function undup (l : list t) : list t
  variant { l }
  ensures { result = U.undup l }
  =
    match l with
    | Nil -> Nil
    | Cons e tl ->
        if mem e tl then undup tl else Cons e (undup tl)
    end

  (* ------------------------------------------------------------------------ *)
  predicate unduped (l : list t) = undup l = l

end

module ListUtils
  use int.Int
  use list.List
  use list.Mem
  use list.Nth
  use list.Length
  use list.Reverse
  use option.Option

  (* ------------------------------------------------------------------------ *)
  let rec nth (i : int) (l : list 't) : option 't
  variant { l }
  ensures { forall k. result = Some k -> mem k l }
  ensures { not (0 <= i < length l) -> result = None }
  ensures { result = nth i l }
  =
  match l with
  | Cons k tl -> if i = 0 then Some k else nth (i - 1) tl
  | Nil -> None
  end

  (* ------------------------------------------------------------------------ *)
  lemma nth_con : forall k : int. nth 0 (Cons k Nil) = Some k

  (* ------------------------------------------------------------------------ *)
  let rec function head (n : int) (l : list 't) : list 't
  variant { l }
  ensures { forall k. mem k result -> mem k l }
  ensures { 0 <= n <= length l -> length result = n }
  ensures { n > length l -> result = l }
  ensures { n < 0 -> result = Nil }
  =
  match l with
  | Cons e tl ->
    if 0 < n then
      Cons e (head (n-1) tl)
    else Nil
  | Nil -> Nil
  end

  (* ------------------------------------------------------------------------ *)
  let rec function tail (n : int) (l : list 't) : list 't
  variant { l }
  ensures { forall k. mem k result -> mem k l }
  ensures { 0 <= n <= length l -> length result = n }
  = reverse (head n (reverse l))

end

(* Collection Set is a fset with get, add and remove ------------------------ *)
module CollectionSet
  use int.Int
  use list.List
  use list.Mem as M
  use option.Option

  (* ------------------------------------------------------------------------ *)
  type t (* asset *)

  (* ------------------------------------------------------------------------ *)
  type tk (* key *)

  (* ------------------------------------------------------------------------ *)
  val function proj t : tk

  (* ------------------------------------------------------------------------ *)
  val function eqt t t : bool

  (* ------------------------------------------------------------------------ *)
  val function eqk tk tk : bool

  (* ------------------------------------------------------------------------ *)
  axiom eqP : forall x y. eqt x y <-> x = y

  (* ------------------------------------------------------------------------ *)
  axiom eqkP : forall x y. eqk x y <-> x = y

  (* ------------------------------------------------------------------------ *)
  val function norm (list t) : list t

  (* ------------------------------------------------------------------------ *)
  clone fset.FSet as F  with  type t     = t,
                              val  eqt   = eqt,
                              val  norm  = norm,
                              axiom .
  (* ------------------------------------------------------------------------ *)
  type cset = F.fset

  (* ------------------------------------------------------------------------ *)
  predicate mem (e : t) (s : cset) = F.fmem e s

  (* ------------------------------------------------------------------------ *)
  let function elts (s : cset) = F.content s

  (* ------------------------------------------------------------------------ *)
  let function mk (l : list t) = F.mk l

  (* GET INTERNAL ----------------------------------------------------------- *)
  let rec function internal_get (k : tk) (l : list t) : option t
  variant { l }
  ensures { forall e. result = Some e -> proj e = k }
  ensures { result = None -> forall e. M.mem e l -> proj e <> k }
  =
  match l with
  | Cons e tl ->
    if eqk (proj e) k
    then Some e
    else internal_get k tl
  | Nil -> None
  end

  (* GET -------------------------------------------------------------------- *)
  let function get (k : tk) (s : cset) : option t
  ensures { result = None -> forall e. F.fmem e s -> proj e <> k }
  ensures { forall e. result = Some e -> proj e = k }
  = internal_get k (F.content s)

  (* ADD -------------------------------------------------------------------- *)
  let function add (a : t) (s : cset) : cset
  ensures { forall k. k <> proj a -> get k result = get k s }
  ensures { get (proj a) result = Some a }
  = F.mk (Cons a (F.content s))  (* normalisation is done by F.mk *)

  (* REMOVE ----------------------------------------------------------------- *)
  let function remove (k : tk) (s : cset) : cset
  ensures { forall e. e <> k -> get e result = get e s }
  ensures { get k result = None }
  =
  let rec internal_remove (l : list t) : list t
  variant { l }
  ensures { forall e. e <> k -> internal_get e result = internal_get e l }
  ensures { internal_get k result = None }
  = match l with
  | Cons e tl ->
    if eqk (proj e) k then tl
    else Cons e (internal_remove tl)
  | Nil -> Nil
  end in
  F.mk (internal_remove (F.content s))

  (* ------------------------------------------------------------------------ *)
  function union (s1 s2 : cset) : cset = F.fsetU s1 s2

  (* ------------------------------------------------------------------------ *)
  function inter (s1 s2 : cset) : cset = F.fsetI s1 s2

  (* ------------------------------------------------------------------------ *)
  function diff (s1 s2 : cset) : cset = F.fsetD s1 s2

  (* ------------------------------------------------------------------------ *)
  function singleton (a : t) : cset = F.mk (Cons a Nil)

  (* ------------------------------------------------------------------------ *)
  let function empty = F.fset0

  (* ------------------------------------------------------------------------ *)
  predicate is_empty (s : cset) = F.content s = Nil

  (* ------------------------------------------------------------------------ *)
  predicate subset (s1 s2 : cset) = F.fsubset s1 s2

  (* ------------------------------------------------------------------------ *)
  predicate (==) (s1 s2 : cset) = F.(==) s1 s2

  (* ------------------------------------------------------------------------ *)
  let function card (s : cset) = F.fcard s

end

(* VIEW is a unduped fset of key -------------------------------------------- *)
module View
  use int.Int
  use list.List
  use list.Mem as M
  use list.Length as L
  use list.Distinct
  use option.Option
  use Types
  use ListUtils as I

  (* ------------------------------------------------------------------------ *)
  type tk

  (* ------------------------------------------------------------------------ *)
  val function eqk tk tk : bool

  (* ------------------------------------------------------------------------ *)
  lemma eqP : forall k1 k2: tk. eqk k1 k2 <-> k1 = k2

  (* ------------------------------------------------------------------------ *)
  clone Undup as U with type t = tk,
                        val eqt = eqk

  (* ------------------------------------------------------------------------ *)
  predicate unduped (l : list tk) = l = U.undup l

  (* ------------------------------------------------------------------------ *)
  type view = {
    content : list tk
  } invariant {
    unduped content
  } by { content = Nil }

  (* ------------------------------------------------------------------------ *)
  let function elts (v : view)
  ensures { unduped result }
  = content v

  (* ------------------------------------------------------------------------ *)
  let function contains (k : tk) (v:view) : bool
  ensures { result <-> M.mem k (elts v) }
  = U.mem k (elts v)

  (* ------------------------------------------------------------------------ *)
  let function card (v : view) : int
  ensures { result = L.length (elts v) }
  =
  L.length (elts v)

  (* ------------------------------------------------------------------------ *)
  lemma undupI : forall l. U.unduped (U.undup l)

  (* ------------------------------------------------------------------------ *)
  let function mk (l: list tk)
  = { content = U.undup l }

  (* ------------------------------------------------------------------------ *)
  let function empty = mk Nil

  (* ------------------------------------------------------------------------ *)
  predicate is_empty (v : view) = v = empty

  (* ------------------------------------------------------------------------ *)
  let function nth (i : int) (v : view) : option tk
  ensures { 0 <= i < card v -> forall k. result = Some k -> contains k v }
  ensures { not (0 <= i < card v) -> result = None }
  = I.nth i (elts v)

  (* ------------------------------------------------------------------------ *)
  lemma nth_singleton: forall k. nth 0 (mk (Cons k Nil)) = Some k

  (* ------------------------------------------------------------------------ *)
  lemma undup_head : forall k l. U.unduped l -> U.unduped (I.head k l)

  (* ------------------------------------------------------------------------ *)
  lemma eqview : forall v1 v2. content v1 = content v2 -> v1 = v2

  (* KEEP first i elements -------------------------------------------------- *)
  let function head (i : int) (v : view) : view
  ensures { forall k. contains k result -> contains k v }
  ensures { 0 <= i <= card v -> card result = i }
  ensures { i > card v -> result = v }
  = mk (I.head i (elts v))

  (* ------------------------------------------------------------------------ *)
  lemma keep_none: forall v. head 0 v = mk Nil

  (* ------------------------------------------------------------------------ *)
  lemma undup_tail : forall k l. U.unduped l -> U.unduped (I.tail k l)

  (* ------------------------------------------------------------------------ *)
  (* keeps last i elements *)
  let function tail (i : int) (v : view) : view
  ensures { forall k. contains k result -> contains k v }
  ensures { 0 <= i <= card v -> card result = i }
  = mk (I.tail i (elts v))

  (* ------------------------------------------------------------------------ *)
  lemma drop_none: forall v. tail 0 v = mk Nil

  (* ------------------------------------------------------------------------ *)
  let function eq (v1 v2 : view) : bool
  =
  let rec function eql l1 l2 : bool
  variant { l1, l2 }
  ensures { result <-> l1 = l2 }
  =
  match l1, l2 with
  | Cons e1 tl1, Cons e2 tl2 ->
    if eqk e1 e2 then eql tl1 tl2 else false
  | Nil, Nil -> true
  | _ -> false
  end in
  eql v1.content v2.content

  (* ------------------------------------------------------------------------ *)
  lemma eqvP : forall v1 v2. eq v1 v2 <-> v1 = v2

end

(* FIELD is a sorted and unduped fset of key -------------------------------- *)
module Field
  use int.Int
  use list.List
  use list.Mem as M
  use Types
  use View as V

  (* ------------------------------------------------------------------------ *)
  type tk

  (* ------------------------------------------------------------------------ *)
  val function eqk tk tk : bool

  (* ------------------------------------------------------------------------ *)
  axiom eqP : forall k1 k2. eqk k1 k2 <-> k1 = k2

  (* ------------------------------------------------------------------------ *)
  val function lek tk tk : bool

  (* ------------------------------------------------------------------------ *)
  let function proj (x : tk) = x

  (* ------------------------------------------------------------------------ *)
  let predicate le k1 k2 = lek k1 k2

  (* ------------------------------------------------------------------------ *)
  clone InsertionSort as S with type elt = tk,
                                val le = le

  (* ------------------------------------------------------------------------ *)
  clone Undup as U with type t = tk,
                        val eqt = eqk

  (* ------------------------------------------------------------------------ *)
  let function norm (l : list tk) = S.insertion_sort (U.undup l)

  (* ------------------------------------------------------------------------ *)
  clone CollectionSet as N with type t     = tk,
                                type tk    = tk,
                                val proj   = proj,
                                val  eqt   = eqk,
                                val  norm  = norm,
                                axiom .

  (* ------------------------------------------------------------------------ *)
  type field = N.cset

  (* ------------------------------------------------------------------------ *)
  predicate mem (k : tk) (f : field) = N.mem k f

  (* ------------------------------------------------------------------------ *)
  let function elts (f : field) = N.elts f

  (* ------------------------------------------------------------------------ *)
  let function empty = N.empty

  (* ------------------------------------------------------------------------ *)
  predicate is_empty (f : field) = N.is_empty f

  (* ------------------------------------------------------------------------ *)
  let function eq (f1 f2 : field) : bool
  =
  let rec internal_eq (l1 l2 : list tk) : bool
  variant { l1, l2 }
  = match l1,l2 with
  | Cons k1 tl1, Cons k2 tl2 ->
    if eqk k1 k2 then
      internal_eq tl1 tl2
    else false
  | Nil,Nil -> true
  | _ -> false
  end in
  internal_eq (N.elts f1) (N.elts f2)

  (* ------------------------------------------------------------------------ *)
  let function add (k : tk) (f : field) = N.add k f

  (* ------------------------------------------------------------------------ *)
  let function remove (k : tk) (f : field) = N.remove k f

  (* ------------------------------------------------------------------------ *)
  type view

  (* ------------------------------------------------------------------------ *)
  val function vmk (list tk) : view

  (* ------------------------------------------------------------------------ *)
  val function velts view : list tk

  (* ------------------------------------------------------------------------ *)
  val function vcontains tk view : bool

  (* ------------------------------------------------------------------------ *)
  let function to_view (f : field) : view
  ensures { S.sorted (velts result) }
  ensures { forall k. vcontains k result <-> M.mem k (N.elts f) }
  = vmk (N.elts f)

end

(* COLLECTION is an unduped fset of asset
   plus an invariant on key injectivity ------------------------------------- *)
module Collection
  use Types
  use int.Int
  use list.List
  use list.Mem as M
  use option.Option
  use View as V
  use fset.ListFilter as F
  use ListUtils as I

  (* ------------------------------------------------------------------------ *)
  type tk

  (* ------------------------------------------------------------------------ *)
  val function eqk tk tk : bool

  (* ------------------------------------------------------------------------ *)
  axiom eqkP : forall k1 k2. eqk k1 k2 <-> k1 = k2

  (* ------------------------------------------------------------------------ *)
  val function lek tk tk : bool

  (* ------------------------------------------------------------------------ *)
  type t

  (* ------------------------------------------------------------------------ *)
  val function keyt t : tk

  (* ------------------------------------------------------------------------ *)
  val function eqt t t : bool

  (* ------------------------------------------------------------------------ *)
  axiom eqtP : forall t1 t2. eqt t1 t2 <-> t1 = t2

  (* ------------------------------------------------------------------------ *)
  let predicate le t1 t2 = lek (keyt t1) (keyt t2)

  (* ------------------------------------------------------------------------ *)
  clone InsertionSort as S with type elt = t,
                                val le = le

  (* ------------------------------------------------------------------------ *)
  clone Undup as U with type t = t,
                        val eqt = eqt

  (* ------------------------------------------------------------------------ *)
  let function norm (l : list t) = S.insertion_sort (U.undup l)

  (* ------------------------------------------------------------------------ *)
  clone CollectionSet as N with type tk    = tk,
                                type t     = t,
                                val proj   = keyt,
                                val eqt    = eqt,
                                val eqk    = eqk,
                                val norm   = norm,
                                axiom .

  (* ------------------------------------------------------------------------ *)
  predicate key_injective (l : list t) = forall a1 a2.
    M.mem a1 l -> M.mem a2 l -> keyt a1 = keyt a2 -> eqt a1 a2

  (* ------------------------------------------------------------------------ *)
  type collection = {
    content : N.cset
  } invariant {
    key_injective (N.elts content)
  } by {
    content = N.empty;
  }

  (* ------------------------------------------------------------------------ *)
  let function elts (c : collection) : list t
  (* TODO : add postconditions *)
  =  N.elts c.content

  (* ------------------------------------------------------------------------ *)
  predicate mem (a : t) (c : collection) = N.mem a c.content

  (* ------------------------------------------------------------------------ *)
  let function mk (s : N.cset) : collection
  requires { key_injective (N.elts s) }
  = { content = s }

  (* ------------------------------------------------------------------------ *)
  let function from_list (l : list t) : collection
  requires { key_injective l }
  = { content = N.mk l }

  (* ------------------------------------------------------------------------ *)
  let function get (k : tk) (c : collection) : option t
  ensures { result = None -> forall e. mem e c -> keyt e <> k }
  ensures { forall e. result = Some e -> keyt e = k }
  ensures { forall e. result = Some e <-> mem e c }
  = N.get k c.content

  (* ------------------------------------------------------------------------ *)
  let function contains (k : tk) (c : collection) : bool
  =
  match get k c with
  | Some _ -> true
  | None -> false
  end

  (* ------------------------------------------------------------------------ *)
  let get_force (k : tk) (c : collection) : t
  raises { NotFound }
  ensures { keyt result = k }
  ensures { mem result c }
  = match get k c with
    | Some v -> v
    | None -> raise NotFound
    end

  (* ------------------------------------------------------------------------ *)
  lemma cont_mk [@rewrite] : forall l. N.elts (N.mk l) = norm l

  (* ------------------------------------------------------------------------ *)
  lemma intget_cons [@rewrite] : forall l [@induction].
    forall e k. keyt e <> k -> N.internal_get k (Cons e l) = N.internal_get k l

  (* ------------------------------------------------------------------------ *)
  lemma cons_key_injective : forall a l.
  key_injective l -> (forall e. M.mem e l -> keyt e <> keyt a) ->
  key_injective (Cons a l)

  (* ------------------------------------------------------------------------ *)
  let function add (a : t) (c : collection) : collection
  ensures { mem a result }
  ensures { forall k. k <> keyt a -> get k c = get k result }
  ensures { get (keyt a) result = Some a }
  =
  mk (N.add a c.content)

  (* ------------------------------------------------------------------------ *)
  let function remove (k : tk) (c : collection) : collection
  ensures { forall i. not (eqk i k) -> get i c = get i result }
  ensures { get k result = None }
  =
  mk (N.remove k c.content)

  (* ------------------------------------------------------------------------ *)
  let function set (k : tk) (a : t) (c : collection) : collection
  requires { keyt a = k }
  requires { get k c <> None }
  ensures { get k result = Some a }
  ensures { forall i. i <> k -> get i result = get i c }
  =
  let rec internal_set (l : list t) : list t
  requires { key_injective l }
  variant { l }
  ensures { key_injective result }
  ensures { N.internal_get k result = Some a }
  ensures { forall i. i <> k -> N.internal_get i result = N.internal_get i l }
  =
  match l with
  | Cons e tl ->
    if eqk (keyt e) k
    then Cons a (internal_set tl)
    else Cons e (internal_set tl)
  | Nil -> Nil
  end in
  mk (N.mk (internal_set (N.elts c.content)))

  (* ------------------------------------------------------------------------ *)
  type view

  (* ------------------------------------------------------------------------ *)
  val function vmk (list tk) : view

  (* ------------------------------------------------------------------------ *)
  val function velts view : list tk

  (* ------------------------------------------------------------------------ *)
  val function vcontains tk view : bool

  (* ------------------------------------------------------------------------ *)
  let function list_to_view (l : list t) : view
  ensures { forall k. vcontains k result <-> forall e. M.mem e l -> keyt e = k }
  =
  let rec function internal_list_to_view (il : list t) : list tk
  variant { l }
  ensures { forall k. M.mem k result <-> forall e. M.mem e l -> keyt e = k }
    =
  match il with
  | Cons e tl -> Cons (keyt e) (internal_list_to_view tl)
  | Nil -> Nil
  end in
  vmk (internal_list_to_view l)

  (* ----------------------------------------------------------------------- *)
  let function view_to_list (v : view) (c : collection) : list t
  =
  let rec internal_view_to_list (l : list tk) : list t
  variant { l }
  =
  match l with
  | Cons k tl ->
    match get k c with
    | Some e -> Cons e (internal_view_to_list tl)
    | None -> internal_view_to_list tl
    end
  | Nil -> Nil
  end in
  internal_view_to_list (velts v)

  (* ----------------------------------------------------------------------- *)
  let function from_view (v : view) (c : collection) : collection
  = mk (N.mk (view_to_list v c))

  (* ----------------------------------------------------------------------- *)
  let predicate le_key k1 k2 = lek k1 k2

  (* ----------------------------------------------------------------------- *)
  clone InsertionSort as K with type elt = tk,
                                val le = le_key

  (* ----------------------------------------------------------------------- *)
  let rec function to_view (c : collection) : view
  ensures { K.sorted (velts result) }
  ensures { forall k. vcontains k result <-> forall e. get k c = Some e <-> keyt e = k }
  =
  list_to_view (N.elts c.content)

  (* ------------------------------------------------------------------------ *)
  function union (c1 c2 : collection) : collection
  = mk (N.union c1.content c2.content)

  (* ------------------------------------------------------------------------ *)
  function inter (c1 c2 : collection) : collection
  = mk (N.inter c1.content c2.content)

  (* ------------------------------------------------------------------------ *)
  function diff (c1 c2 : collection) : collection
  = mk (N.diff c1.content c2.content)

  (* ------------------------------------------------------------------------ *)
  function singleton (a : t) : collection = mk (N.singleton a)

   (* ------------------------------------------------------------------------ *)
  let function empty = mk N.empty

  (* ------------------------------------------------------------------------ *)
  predicate is_empty (c : collection) = N.is_empty c.content

  (* ------------------------------------------------------------------------ *)
  predicate subset (c1 c2 : collection) = N.subset c1.content c2.content

  (* ------------------------------------------------------------------------ *)
  predicate (==) (c1 c2 : collection) = N.(==) c1.content c2.content

  (* ------------------------------------------------------------------------ *)
  let function card (c : collection) = N.card c.content

  (* ------------------------------------------------------------------------ *)
  function select (p : t -> bool) (c : collection) : collection
  = mk (N.mk (F.filter p ((N.elts (content c)))))

  (* ------------------------------------------------------------------------ *)
  lemma mem_select : forall e c p.
  mem e (select p c) <-> mem e c /\ p e

  (* ------------------------------------------------------------------------ *)
  let function head (i : int) (c : collection) : collection
  ensures { forall e. mem e result -> mem e c }
  ensures { 0 <= i <= card c -> card result = i }
  ensures { i > card c -> result = c }
  = mk (N.mk (I.head i (N.elts c.content)))

  (* ------------------------------------------------------------------------ *)
  let function tail (i : int) (c : collection) : collection
  ensures { forall e. mem e result -> mem e c }
  ensures { 0 <= i <= card c -> card result = i }
  = mk (N.mk (I.tail i (N.elts c.content)))

  (* ------------------------------------------------------------------------ *)
  type field

  (* ------------------------------------------------------------------------ *)
  val function felts field : list tk

  (* ------------------------------------------------------------------------ *)
  let function from_field (f : field) (c : collection) : collection
  =
  let rec internal_from_field (l : list tk) : list t
  variant { l }
  =
  match l with
  | Cons k tl ->
    match get k c with
    | Some a -> Cons a (internal_from_field tl)
    | None -> internal_from_field tl
    end
  | Nil -> Nil
  end in
  mk (N.mk (internal_from_field (felts f)))

end

module Set
  use list.List

  (* ------------------------------------------------------------------------ *)
  type t

  (* ------------------------------------------------------------------------ *)
  val function eqt t t : bool

  (* ------------------------------------------------------------------------ *)
  val function le_t t t : bool

  (* ------------------------------------------------------------------------ *)
  axiom eqP : forall x y. eqt x y <-> x = y

  (* ------------------------------------------------------------------------ *)
  let predicate le t1 t2 = le_t t1 t2

  (* ------------------------------------------------------------------------ *)
  clone InsertionSort as S with type elt = t,
                                val le = le

  (* ------------------------------------------------------------------------ *)
  clone Undup as U with type t = t,
                        val eqt = eqt

  (* ------------------------------------------------------------------------ *)
  let function norm (l : list t) = S.insertion_sort (U.undup l)

  (* ------------------------------------------------------------------------ *)
  clone fset.FSet as F with type t     = t,
                            val eqt    = eqt,
                            val norm   = norm,
                            axiom .

  (* ------------------------------------------------------------------------ *)
  type set = F.fset

  (* ------------------------------------------------------------------------ *)
  let function add (e : t) (s : set) : set = F.mk (Cons e (F.content s))

  (* ------------------------------------------------------------------------ *)
  let function contains (e : t) (s : set) : bool
  =
  let rec function internal_contains (l : list t) : bool
  variant { l }
  = match l with
  | Cons x tl ->
    if eqt e x then true else internal_contains tl
  | Nil -> false
  end in
  internal_contains (F.content s)

  (* ------------------------------------------------------------------------ *)
  let function remove (e : t) (s : set) : set
  =
  let rec function internal_remove (l : list t) : list t
  variant { l }
  = match l with
  | Cons x tl ->
    if eqt e x then tl else internal_remove tl
  | Nil -> Nil
  end in
  F.mk (internal_remove (F.content s))

  (* ------------------------------------------------------------------------ *)
  let function card (s : set) : int = F.fcard s

  (* ------------------------------------------------------------------------ *)
  let function empty = F.fset0

  (* ------------------------------------------------------------------------ *)
  let function elts (s : set) : list t
  =
  F.content s

  (* ------------------------------------------------------------------------ *)
  let function from_list (l : list t) = F.mk l

end

module List
  use list.List
  use list.Length as L
  use list.Nth as N
  use option.Option
  use Types

  (* ------------------------------------------------------------------------ *)
  type t

  (* ------------------------------------------------------------------------ *)
  val function eqt t t : bool

  (* ------------------------------------------------------------------------ *)
  axiom eqP : forall x y. eqt x y <-> x = y

  (* ------------------------------------------------------------------------ *)
  let function card l = L.length l

  (* ------------------------------------------------------------------------ *)
  let nth n l
  raises { NotFound }
  =
  match N.nth l n with
  | Some e -> e
  | None -> raise NotFound
  end

  (* ------------------------------------------------------------------------ *)
  let rec function contains a l
  =
  match l with
  | Nil -> false
  | Cons e tl -> eqt a e || contains a tl
  end

  (* ------------------------------------------------------------------------ *)
  let function prepend (a : t) (l : list t) : list t
  = Cons a l

  (* ------------------------------------------------------------------------ *)
  let rec function eq_list (l1 : list t) (l2 : list t) : bool
  variant { l1, l2 }
  =
  match l1, l2 with
  | Cons e1 tl1, Cons e2 tl2 ->
    if eqt e1 e2 then true else eq_list tl1 tl2
  | _ -> false
  end

  (* ------------------------------------------------------------------------ *)
  lemma eq_list_spec : forall l1 l2. eq_list l1 l2 <-> l1 = l2

end

module Sort
  use list.List
  use int.Int
  use Types
  use View as V

  (* ------------------------------------------------------------------------ *)
  type t
  (* ------------------------------------------------------------------------ *)
  type view
  (* ------------------------------------------------------------------------ *)
  type collection
  (* ------------------------------------------------------------------------ *)
  val function cmp t t : bool
  (* ------------------------------------------------------------------------ *)
  val function view_to_list view collection : list t
  (* ------------------------------------------------------------------------ *)
  val function list_to_view (list t) : view

  (* ------------------------------------------------------------------------ *)
  let predicate ple (a b : t) = cmp a b

  (* ------------------------------------------------------------------------ *)
  clone InsertionSort as IS with type elt = t,
                                 val le = ple

  let function sort (v : view) (c : collection)
  =
  list_to_view (IS.insertion_sort (view_to_list v c))

end

module Sum

  use  int.Int
  use  list.List
  use  option.Option
  use  View as V

  (* ------------------------------------------------------------------------ *)
  type t

  (* ------------------------------------------------------------------------ *)
  type tk

  (* ------------------------------------------------------------------------ *)
  type collection

  (* ------------------------------------------------------------------------ *)
  type view

  (* ------------------------------------------------------------------------ *)
  val function field t : int

  (* ------------------------------------------------------------------------ *)
  val function view_to_list view collection : list t

  (* ------------------------------------------------------------------------ *)
  val function from_view view collection : collection

  (* ------------------------------------------------------------------------ *)
  let function sum (v : view) (c : collection) : int
  =
  let rec function internal_sum (l : list t) : int
  variant { l }
  =
    match l with
     | Nil -> 0
     | Cons e tl -> field e + internal_sum tl
    end
  in
  internal_sum (view_to_list v c)

  (* ------------------------------------------------------------------------ *)
  function sum_formula (c : collection) : int

  (* ------------------------------------------------------------------------ *)
  axiom sum_formula_spec : forall v c. sum v c = sum_formula (from_view v c)

end

theory Lib

  use export int.Int
  use export option.Option
  use export Utils
  use export Types
  use export Operation
  use export Trace

end