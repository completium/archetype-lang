archetype escrow_with_object

variable buyer : role

variable[%transferable%] debitor : role = buyer

variable seller : role

variable[%transferable%] creditor : role = seller

variable oracle : role

variable[%traceable%] price : tez

variable[%traceable%] [%mutable_signed ([buyer, debitor], instate (Created))%]
     penalty : tez = 0.1 * price

(* action deadline *)
variable[%mutable (buyer or seller, instate (Created))%] deadline : date

(* type taskStatus {
     date : date;
     status : string of [ "OK" | "KO" ]
   }
*)
variable[%signedby oracle%] taskStatus : object = "https://oracle.io/tskstat.schema.json"

(* state machine *)
states =
 | Created initial
 | Aborted
 | Confirmed
 | Canceled    with { s1 : balance = 0tz; }
 | Transferred with { s2 : transferred_to (price, buyer);
                      s3 : balance = 0tz;
                    }

transition abort from Created {
  called by buyer or seller

  to Aborted
}

transition[%signedbyall [{buyer}; {seller}]%] confirm from Created {
  to Confirmed when { balance = price + penalty }
}

transition finalize (task : taskStatus) from any {
  to Transferred
  when { task.date <= deadline and task.status }
  with effect {
    transfer price to creditor;
    transfer penalty to seller
  }

  to Canceled
  with effect {
    transfer penalty to debitor;
    transfer price to buyer
  }
}

