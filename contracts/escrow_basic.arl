archetype escrow_basic

variable buyer : role  = @tz1KmuhR6P52hw6xs5P69BXJYAURaznhvN1k

variable seller : role = @tz1XJYAURaznhvN1khR6P52hw6xs5P691Kmu

variable oracle : role = @tz15P69BXJYAURaznhvN1k1KmuhR6P52hw6x

variable price : tez = 10tz

(* action deadline *)
variable deadline : date = 2019-06-01T00:00:00

(* state machine *)
states =
 | Created initial
 | Aborted   with { i1 : balance = 0tz; }
 | Confirmed with { i2 : balance = price; }
 | Canceled  with { i3 : balance = 0tz; }
 | Completed with { i4 : balance = 0tz; }

transition abort from Created {
  called by buyer or seller

  to Aborted
}

transition confirm from Created {
  to Confirmed when { balance = price }
}

transition[%onlyonce%] complete from Confirmed {
  called by oracle

  to Completed when { now < deadline }
  with effect {
    transfer price to seller
  }
}

transition[%onlyonce%] cancel from Confirmed {
  called by oracle

  to Canceled
  with effect {
    transfer price to buyer
  }
}

(* security {
  s1 : only_by_role (transfers, oracle)
} *)
