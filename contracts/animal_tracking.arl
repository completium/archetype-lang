archetype animal_tracking

enum animal_e =
 | Sheep
 | Cattle
 | Pig
 | Other

enum movement_e =
 | In_field initial
 | In_transit

asset business_a identified by id {
  id        : string;
  incomings : animal_a collection;
}

asset field_a identified by name {
  name     : string;
  business : business_a;
}

asset animal_a identified by ida {
 ida      : string;
 typ      : animal_e;
 location : field_a;
} with states movement_e

transition transit (fk : string) on (ak : pkey of animal_a) from In_field {
 to In_transit
 with effect {
   field_a.get(fk).business.incomings.add(animal_a.get(ak))
 }
}

transition arrival (toField : field_a) on (ak : pkey of animal_a) from In_transit {
  to In_field
  with effect {
    animal_a.get(ak).location := toField;
    toField.business.incomings.remove(ak)
  }
}
