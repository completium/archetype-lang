archetype escrow_simple

constant buyer : role  = @tz1bfVgcJC4ukaQSHUe1EbrUd5SekXeP9CWk

constant seller : role = @tz1Lc2qBKEWCBeDU8npG6zCeCqpmaegRi6Jg

constant oracle : role = @tz1iawHeddgggn6P5r5jtq2wDRqcJVksGVSa

constant price : tez = 10tz

constant penalty : tez = 2tz

(* action deadline *)
constant deadline : date = 2019-06-01T00:00:00Z

(* state machine *)
states =
 | Created initial
 | Aborted   with { s0 : balance = 0tz }
 | Confirmed with { s1 : balance = price + penalty }
 | Canceled  with { s2 : balance = 0tz }
 | Completed with { s3 : balance = 0tz;
                    (* s4 : transferred_to(seller) = price + penalty; *)
                  }

transition abort () {
  called by buyer or seller

  from Created
  to Aborted
}

transition confirm () {
  from Created
  to Confirmed when { balance = price + penalty }
}

transition complete () {
  called by oracle

  from Confirmed
  to Completed when { now < deadline }
  with effect {
    transfer price to seller;
    transfer penalty to seller
  }
}

transition cancel () {
  called by oracle

  from Confirmed
  to Canceled
  with effect {
    transfer penalty to buyer;
    transfer price to buyer
  }
}
