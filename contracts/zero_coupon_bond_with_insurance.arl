archetype zero_coupon_bond_with_insurance

variable issuer : address = @tz1bfVgcJC4ukaQSHUe1EbrUd5SekXeP9CWk (* seller 'Alice' *)

variable owner : address  = @tz1Lc2qBKEWCBeDU8npG6zCeCqpmaegRi6Jg
(* buyer 'Bob'; receives 11 tez in one-year *)

variable zero_insur : address = @KT1TJrR7uovV5tFpsLBCKmx3x95pY6hMy775

variable price : tez = 10tz

variable payment : tez = 1.1 * price

variable maturity : date = 2020-01-01

states =
 | Created initial
 | Insured   (* Guarantee Fund has accepted issuer *)
 | Confirmed (* owner has purchased bond *)
 | Repaid    (* issuer has transferred payment to contract *)
 | Collected (* owner has collected payment *)

transition insured () {
  called by zero_insur

  from Created
  to Insured
}

transition confirm () {
  from Insured
  to Confirmed
  when { transferred = price }
  with effect {
    maturity := now + 365d;
    transfer price to issuer
  }
}

transition repay () {
  called by issuer

  from Confirmed
  to Repaid
  when {
    transferred = payment
  }
}

transition collect () {
  called by owner

  from Repaid
  to Collected
  when { now > maturity }
  with effect {
    if balance >= payment
    then transfer balance to owner
    else transfer 0tz to zero_insur call pay<address * tez>((owner, payment))
  }
}
