archetype fa12

constant totalsupply : nat = 10_000_000

asset allowance identified by addr_owner addr_spender to big_map {
  addr_owner       : address;
  addr_spender     : address;
  amount           : nat;
}

asset ledger identified by holder to big_map {
  holder     : address;
  tokens     : nat = 0;
} initialized by {
  { holder = caller; tokens = totalsupply }
}

entry %transfer (from_ : address, to_ : address, value : nat) {
  if ledger[from_].tokens < value then
    fail("NotEnoughBalance");
  if caller <> from_ then (
    var current = allowance[(from_, caller)].amount;
    if current < value then
      fail(("NotEnoughAllowance",((value,current))));
    allowance.update((from_, caller), { amount -=  value });
  );
  ledger.update(from_, { tokens -= value });
  ledger.addupdate(to_, { tokens += value });
}

entry approve(spender : address, value : nat) {
  var k = (caller, spender);
  if allowance.contains(k) then (
    var previous = allowance[k].amount;
    if previous > 0 and value > 0
    then fail(("UnsafeAllowanceChange", previous)));
  allowance.addupdate( k, { amount = value });
}

entry getAllowance (owner : address, spender : address, cb : entrysig<nat>) {
  transfer 0tz to entry cb(allowance[(owner,spender)].amount);
}

entry getBalance (owner : address, cb : entrysig<nat>) {
  transfer 0tz to entry cb(ledger[owner].tokens);
}

entry getTotalSupply (cb : entrysig<nat>) {
  transfer 0tz to entry cb(totalsupply);
}

specification {
  s1 :  ledger.sum(tokens) = totalsupply;
}
